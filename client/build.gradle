description = 'GWT client'

apply plugin: 'war'
apply plugin: 'gwt'
if ("${analysis}" == "true") {
    [findbugsMain, findbugsTest]*.enabled = false
}

pmdMain {
    ignoreFailures = true
}
configurations {
    javascript
}

dependencies {
    compile project(':rpc')
    compile project(path: ':rpc', configuration: 'sourceCode')
    compile project(path: ':keyboard-action', configuration: 'sourceCode')
    compile project(':keyboard-action')
    compile "com.google.gwt.inject:gin:1.5.0"
    compile libs.inject
    compile libs.guice
    compile libs.guice_assisted_inject
    compile libs.guava
    compile libs.guava_gwt
    compile "com.google.gwt:gwt-user:${gwt_version}"
    compile "com.reveregroup.gwt:gwt-image-loader:1.1.4"
    compile "com.google.gwt:gwt-dev:${gwt_version}"
    compile "com.google.code.ginmvp:ginmvp-core:1.1"

    testCompile "junit:junit:3.8.1"
    testCompile libs.junit
    testCompile libs.jukito
    testCompile libs.mockito
    testCompile "org.jmock:jmock:2.5.1"
    testCompile libs.gwt_test_utils
}

task convertIcons(type: Exec, dependsOn: classes) {
    inputs.dir "src/main/resources/images/originals"
    outputs.dir "build/classes/main/images/"
    commandLine "./convert_gradle.sh", "${icon_size}"
}

task writeVersion(type: Exec, dependsOn: classes) {
    outputs.file "build/classes/main/version.txt"
    commandLine "./write_version_gradle.sh", "${version}"
}

task javascriptJar(type: Jar, dependsOn: compileGwt) {
    classifier = 'javascript'
    excludes = ['**/WEB-INF/web.xml']
    from "${buildDir}/gwt/out",
            "${rootDir}/client/src/main/webapp",
            "${rootDir}/client/src/main/resources"
}

test {
    exclude '**/*GwtTest*'
    jvmArgs '-Xmx256m', '-XX:+HeapDumpOnOutOfMemoryError'
}
compileGwt.dependsOn convertIcons, writeVersion
gwtDev.dependsOn convertIcons, writeVersion

gwt {
			modules = ['com.googlecode.fspotcloud.FSpotCloud']
}

compileGwt {
    if (Boolean.valueOf("${release}")) {
        style = 'OBF'
    } else {
        style = 'PRETTY'
    }
    compileReport = Boolean.valueOf("${gwt_compile_report}")
}

gwtDev {
    startupUrl  "http://localhost:8080/FSpotCloud.html"
}

artifacts {
    javascript javascriptJar
}
